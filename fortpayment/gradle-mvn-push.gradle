import org.gradle.internal.credentials.DefaultAwsCredentials
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
ext{
    VERSION = "2.0.3"
    if(project.hasProperty("maven_version")){
        VERSION = project.property("maven_version")
    }
}
// update these next lines to fit your submodule
group = 'com.amazon'
version = ext.VERSION
// Add sources as an artifact
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

// Loop over all variants
android.libraryVariants.all { variant ->
    if (variant.buildType.name == "debug") return // Prevents publishing debug library
    variant.outputs.all { output ->
        // This creates a publication for each variant
        publishing.publications.create(variant.name, MavenPublication) {
            // The sources artifact from earlier
            //   artifact sourceJar

            // Variant dependent artifact, e.g. release, debug
            artifact source: output.outputFile, classifier: output.name

            // Go through all the dependencies for each variant and add them to the POM
            // file as dependencies
            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')

                // Filter out anything that's not an external dependency. You shouldn't
                // be publishing artifacts that depend on local (e.g. project) dependencies,
                // but who knows...
                configurations.getByName(variant.name + "CompileClasspath").allDependencies
                        .findAll { it instanceof ExternalDependency }
                        .each {
                            def dependency = dependencies.appendNode('dependency')

                            dependency.appendNode('groupId', it.group)
                            dependency.appendNode('artifactId', it.name)
                            dependency.appendNode('version', it.version)
                        }
            }
        }
    }
}

// Ensure that the publish task depends on assembly
tasks.all { task ->
    if (task instanceof AbstractPublishToMaven) {
        task.dependsOn assemble
    }
}


// Configure the destination repository with
// S3 URL and access credentials
publishing {
    repositories {
        maven {
            url 's3://android-mobile-sdk-mvn.s3-us-east-1.amazonaws.com'
            authentication {
                awsIm(AwsImAuthentication)
            }
        }
    }
}






